/* homework 4.48, 48_bubble_sort.ys */

# Execution begins at address 0
    .pos 0
    irmovq stack, %rsp
    call main
    halt

# Array of 4 elements
    .align 8
array:
    .quad 0x0000000000000003
    .quad 0x0000000000000001
    .quad 0x0000000000000004
array_last:
    .quad 0x0000000000000002

main:
    irmovq array, %rdi
    irmovq array_last, %rsi
    call bubble_sort
    ret

# void bubble_sort(long *data, long *data_last)
# data in %rdi, data_last in %rsi
bubble_sort:
    irmovq $8, %r8
    jmp test1
loop1:
    # i = data, i in %rax
    rrmovq %rdi, %rax
test2:
    # i < last
    rrmovq %rsi, %r9
    subq %rax, %r9
    jle update1
loop2:
	# *(i+1) in %r9, *i in %r10
	mrmovq 8(%rax), %r9
	mrmovq (%rax), %r10
	rrmovq %r9, %r11
    subq %r10, %r11
################################## differences
	# long t = less ? *(i+1) : 0
	cmovl %r9, %r12
	# *(i+1) = less ? *i : *(i+1)
	cmovl %r10, %r9
	# *i = less ? t : *i
	cmovl %r12, %r10
	rmmovq %r9, 8(%rax)
	rmmovq %r10, (%rax)
################################## differences
    # i++
    addq %r8, %rax
    jmp test2
update1:
    # last--
    subq %r8, %rsi
test1:
    # last > data
    rrmovq %rsi, %r9
    subq %rdi, %r9
    jg loop1
    ret

# Stack starts here and grows to lower addresses
    .pos 0x200
stack:
